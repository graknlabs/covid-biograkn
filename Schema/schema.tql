define 

substance sub entity; 

chemical sub substance,
    owns chemical-name; 

variant sub entity,
    owns snpId,
    plays chromosome-location:located-variant;
chromosome sub entity, 
    owns chromosome-name,
    plays chromosome-location:locating-chromosome;

fully-formed-anatomical-structure sub entity;

cell sub fully-formed-anatomical-structure,
    owns cell-name,
    plays expression:expressing-cell,
    plays composition:composing-cell;

tissue sub fully-formed-anatomical-structure,
    owns tissue-name,
    plays tissue-enhancement:enhanced-tissue,
    plays expression:expressing-tissue,
    plays composition:composed-tissue,
    plays process-localisation:tissue-context,
    plays gene-tissue-association:associated-tissue,
    plays disease-tissue-association:associated-tissue,
    plays drug-tissue-association:associated-tissue,
    plays mention:mentioned;

gene sub fully-formed-anatomical-structure,
    owns official-gene-symbol,
    owns alternative-gene-symbol,
    owns entrez-id,
    owns ensembl-gene-stable-id,
    owns ensembl-xref,
    owns gene-name,
    plays expression:expressed-gene,
    plays gene-protein-encoding:encoding-gene,
    plays transcription:transcribing-gene,
    plays mention:mentioned,
    plays gene-disease-association:associated-gene,
    plays gene-tissue-association:associated-gene,
    plays drug-gene-interaction:target-gene,
    plays gene-virus-association:associated-virus-gene,
    plays chromosome-location:located-gene,
    plays inhibition:inhibited,
    plays inhibition:inhibiting,
    plays interaction:interacted,
    plays interaction:interacting,
    plays coexistance:coexisting,
    plays comparison:comparing,
    plays comparison:compared,
    plays comparison:higher,
    plays comparison:lower,
    plays stimulation:stimulating,
    plays stimulation:stimulated,
    plays conversion:converting,
    plays conversion:converted,
    plays production:producing,
    plays production:produced,
    plays neg-coexistance:neg-coexisting,
    plays neg-inhibition:neg-inhibited,
    plays neg-inhibition:neg-inhibiting,
    plays neg-interaction:neg-interacted,
    plays neg-interaction:neg-interacting,
    plays neg-stimulation:neg-stimulating,
    plays neg-stimulation:neg-stimulated,
    plays neg-production:neg-producing,
    plays neg-production:neg-produced,
    plays neg-constitution:neg-constituting,
    plays neg-constitution:neg-constituted,
    plays similarity:similar,
    plays neg-similarity:neg-similar,
    plays location:locating,
    plays located-in:locating,
    plays location:located,
    plays located-in:located,
    plays constitution:constituting,
    plays constitution:constituted,
    plays neg-comparison:neg-higher,
    plays neg-comparison:neg-lower,
    plays neg-conversion:neg-converting,
    plays neg-conversion:neg-converted,
    plays disruption:disrupting,
    plays disruption:disrupted,
    plays augmentation:augmenting,
    plays augmentation:augmented,
    plays affection:affecting,
    plays affection:affected,
    plays association:associated,
    plays association:associating;
transcript sub fully-formed-anatomical-structure, 
    owns ensembl-transcript-stable-id, 
    plays transcription:encoded-transcript,
    plays translation:translating-transcript,
    plays mention:mentioned;
protein sub chemical,
    owns uniprot-id,
    owns uniprot-entry-name,
    owns uniprot-symbol,
    owns primary-uniprot-name,
    owns alternative-uniprot-name,
    owns ensembl-protein-stable-id,
    owns go-evidence-code,
    owns go-taxon,
    owns go-id,
    owns function-description,
    plays modulation:modulated,
    plays protein-protein-interaction:activating-protein,
    plays protein-protein-interaction:activated-protein,
    plays translation:translated-protein,
    plays protein-complex:associated-protein,
    plays protein-disease-association:associated-protein, 
    plays mention:mentioned,
    plays gene-protein-encoding:encoded-protein,
    plays tissue-enhancement:expressed-protein,
    plays protein-protein-interaction:interacting-protein,
    plays pathway-participation:participating-protein,
    plays drug-protein-interaction:target-protein,
    plays association:associating,
    plays organism-association:associating,
    plays protein-virus-association:hosting-virus-protein;
pathological-function sub entity;
cell-function sub pathological-function,
    owns cell-function-name;
disease sub pathological-function,
    owns disease-name,
    owns disease-id,
    owns disease-type,
    plays mention:mentioned,
    plays disease-class-association:categorised-disease,
    plays protein-disease-association:associated-disease,
    plays gene-disease-association:associated-disease,
    plays disease-tissue-association:associated-disease,
    plays drug-disease-association:affected-disease;
disease-class sub entity, 
    owns class-name,
    plays disease-class-association:associated-class;
drug sub entity,
    owns drug-name,
    owns chembl-id,
    owns drug-claim-name,
    owns drug-claim-source,
    plays drug-protein-interaction:interacting-drug,
    plays drug-gene-interaction:interacting-drug,
    plays drug-protein-interaction:inhibitor,
    plays drug-protein-interaction:substrate,
    plays drug-protein-interaction:antagonist,
    plays drug-protein-interaction:blocker,
    plays drug-disease-association:therapeutic,
    plays drug-protein-interaction:interacting-drug,
    plays drug-gene-interaction:interacting-drug,
    plays drug-tissue-association:associated-drug,
    plays drug-compound-combination:resulting-drug;
compound sub entity,
    plays drug-compound-combination:participating-compound,
    plays modulation:modulator;
pathway sub entity,
    owns pathway-name,
    owns pathway-id,
    plays pathway-participation:participated-pathway;
organism sub entity,
    owns organism-name,
    plays organism-association:associated-organism,
    plays organism-virus-hosting:hosting-organism;
virus sub organism, 
    owns genbank-id, 
    owns virus-name, 
    owns identity-percentage,
    plays mention:mentioned,
    plays organism-virus-hosting:hosted-virus,
    plays protein-virus-association:associated-virus,
    plays gene-virus-association:associated-virus,
    plays discovery:discovered-virus;
geo-location sub entity,
    owns name, 
    plays located-in:locating,
    plays located-in:located,
    plays discovery:discovering-location;
country sub geo-location,
    owns country-name; 
journal sub entity, 
    owns journal-name,
    plays publishing:publishing-journal; 
publication sub entity, 
    owns paper-id,
    owns title,
    owns doi,
    owns year,
    owns volume, 
    owns issn, 
    owns pmid,
    plays mention:mentioning,
    plays authorship:authored-publication,
    plays publishing:published-publication; 
person sub entity,
    owns published-name,
    plays authorship:author; 

## Relations
transcription sub relation, 
    relates transcribing-gene,
    relates encoded-transcript;
translation sub relation, 
    relates translated-protein,
    relates translating-transcript;
protein-complex sub relation, 
    owns complexportal-name,
    owns complexportal-id,
    owns complexportal-object-symbol,
    owns go-id,
    owns go-taxon,
    owns pmid,
    owns go-evidence-code,
    plays mention:mentioned,
    relates associated-protein;
gene-protein-encoding sub relation,
    relates encoded-protein,
    relates encoding-gene;
protein-disease-association sub relation,
    relates associated-protein,
    relates associated-disease;
gene-disease-association sub relation,
    owns disgenet-score,
    relates associated-gene,
    relates associated-disease;
gene-tissue-association sub relation,
    relates associated-tissue,
    relates associated-gene;
drug-protein-interaction sub relation,
    relates inhibitor,
    relates substrate,
    relates antagonist,
    relates blocker,
    relates interacting-drug,
    relates target-protein;
drug-gene-interaction sub relation,
    owns interaction-type,
    relates target-gene,
    relates interacting-drug;
tissue-enhancement sub relation,
    relates enhanced-tissue,
    relates expressed-protein;
process-localisation sub relation,
    relates biomolecular-process,
    relates tissue-context;
protein-protein-interaction sub relation,
    relates interacting-protein,
    relates activating-protein,
    relates activated-protein,
    plays process-localisation:biomolecular-process;
drug-disease-association sub relation,
    relates affected-disease,
    relates therapeutic;
pathway-participation sub relation,
    relates participating-protein,
    relates participated-pathway;
disease-tissue-association sub relation,
    relates associated-disease,
    relates associated-tissue;
drug-tissue-association sub relation,
    relates associated-drug,
    relates associated-tissue;
drug-compound-combination sub relation, 
    relates participating-compound,
    relates resulting-drug;
modulation sub relation,
    relates modulator,
    relates modulated;
organism-association sub relation, 
    relates associated-organism,
    relates associating;
organism-virus-hosting sub relation,
    relates hosted-virus,
    relates hosting-organism;
protein-virus-association sub relation,
    relates associated-virus,
    relates hosting-virus-protein;
gene-virus-association sub relation, 
    relates associated-virus,
    relates associated-virus-gene;
discovery sub relation,
    relates discovered-virus,
    relates discovering-location;
chromosome-location sub relation,
    owns start-location, 
    owns end-location,
    relates located-variant,
    relates located-gene,
    relates locating-chromosome;
disease-class-association sub relation,
    relates categorised-disease,
    relates associated-class; 

expression sub relation, 
    owns expression-value, 
    owns expression-value-reliability,
    relates expressing-tissue,
    relates expressing-cell,
    relates expressed-gene;

composition sub relation,
    relates composed-tissue,
    relates composing-cell;

inhibition sub relation,
    relates inhibited,
    relates inhibiting,
    plays mention:mentioned-genes-relation;

interaction sub relation,
    relates interacted,
    relates interacting,
    plays mention:mentioned-genes-relation;

coexistance sub relation,
    relates coexisting,
    plays mention:mentioned-genes-relation;

comparison sub relation,
    relates compared,
    relates comparing,
    relates higher,
    relates lower,
    plays mention:mentioned-genes-relation;

stimulation sub relation,
    relates stimulated,
    relates stimulating,
    plays mention:mentioned-genes-relation;

conversion sub relation,
    relates converted,
    relates converting,
    plays mention:mentioned-genes-relation;

production sub relation,
    relates producing,
    relates produced,
    plays mention:mentioned-genes-relation;

 neg-coexistance sub relation,
    relates neg-coexisting,
    plays mention:mentioned-genes-relation;

 neg-inhibition sub relation,
    relates neg-inhibited,
    relates neg-inhibiting,
    plays mention:mentioned-genes-relation;

neg-interaction sub relation,
    relates neg-interacted,
    relates neg-interacting,
    plays mention:mentioned-genes-relation;

neg-stimulation sub relation,
    relates neg-stimulated,
    relates neg-stimulating,
    plays mention:mentioned-genes-relation;

neg-production sub relation,
    relates neg-produced,
    relates neg-producing,
    plays mention:mentioned-genes-relation;

neg-constitution sub relation,
    relates neg-constituted,
    relates neg-constituting,
    plays mention:mentioned-genes-relation;

similarity sub relation,
    relates similar,
    plays mention:mentioned-genes-relation;

neg-similarity sub relation,
    relates neg-similar,
    plays mention:mentioned-genes-relation;

location sub relation,
    relates located,
    relates locating,
    plays mention:mentioned-genes-relation;

constitution sub relation,
    relates constituted,
    relates constituting,
    plays mention:mentioned-genes-relation;

neg-comparison sub relation,
    relates neg-higher,
    relates neg-lower,
    plays mention:mentioned-genes-relation;

neg-conversion sub relation,
    relates neg-converted,
    relates neg-converting,
    plays mention:mentioned-genes-relation;

disruption sub relation,
    relates disrupted,
    relates disrupting,
    plays mention:mentioned-genes-relation;

augmentation sub relation,
    relates augmented,
    relates augmenting,
    plays mention:mentioned-genes-relation;

affection sub relation,
    relates affected,
    relates affecting,
    plays mention:mentioned-genes-relation;

association sub relation,
    relates associated,
    relates associating,
    plays mention:mentioned-genes-relation;

mention sub relation,  
    owns source,
    owns sentence-text,
    relates mentioning-paragraph,  
    relates mentioning,  
    relates mentioned, 
    relates mentioned-genes-relation; 

located-in sub relation, 
    relates locating, 
    relates located;
    
publishing sub relation, 
    relates publishing-journal,
    relates published-publication; 

authorship sub relation,  
    relates author,  
    relates authored-publication;

## Attributes
# Name attributes: 
    chromosome-name sub attribute, value string;
    class-name sub attribute, value string; 
    disease-name sub attribute, value string;
    disease-type sub attribute, value string;
    database-name sub attribute, value string;
    uniprot-name sub attribute, abstract, value string;
    primary-uniprot-name sub uniprot-name, value string;
    alternative-uniprot-name sub uniprot-name, value string;
    gene-symbol sub attribute, abstract, value string;
    official-gene-symbol sub gene-symbol, value string;
    alternative-gene-symbol sub attribute, value string;
    ensembl-xref sub attribute, value string; 
    uniprot-symbol sub attribute, value string; 
    complexportal-name sub attribute, value string;
    complexportal-object-symbol sub attribute, value string;
    gene-name sub attribute, value string;
    protein-name sub attribute, value string;
    journal-name sub attribute, value string;
    cell-function-name sub attribute, value string;
    cell-name sub attribute, value string;
    chemical-name sub attribute, value string;
    uniprot-entry-name sub attribute, value string;
    pathway-name sub attribute, value string;
    tissue-name sub attribute, value string;
    drug-name sub attribute, value string;
    organism-name sub attribute, value string;
    virus-name sub attribute, value string; 
    drug-claim-name sub attribute, value string;
    source sub attribute, value string; 
    pmid sub attribute, value string; 
    paper-id sub identifier;
    title sub attribute, value string; 
    year sub attribute, value long;
    volume sub attribute, value string;
    issn sub attribute, value string; 
    doi sub attribute, value string;


identifier sub attribute, abstract, value string;
    complex-id sub identifier;
    complexportal-id sub identifier;
    gene-id sub identifier, abstract;
        ensembl-gene-stable-id sub gene-id;
        entrez-id sub gene-id;
    protein-id sub identifier, abstract;
        uniprot-id sub protein-id;
        ensembl-protein-stable-id sub protein-id;
        uniprot-id sub protein-id;
        ensembl-id sub protein-id;
    transcript-id sub identifier, abstract;
        ensembl-transcript-stable-id sub transcript-id;
    go-id sub identifier;
    disease-id sub identifier;
    genbank-id sub identifier;

    GEOStudy-id sub identifier;
    GEOComparison-id sub identifier;
    pathway-id sub identifier;
    chembl-id sub identifier;
    snpId sub identifier;



expression-value sub attribute, value string; 
expression-value-reliability sub attribute, value string; 
drug-claim-source sub attribute, value string;
identity-percentage sub attribute, value double;
go-evidence-code sub attribute, value string; 
go-taxon sub attribute, value long; 
function-description sub attribute, value string;
start-location sub attribute, value long;
end-location sub attribute, value long;
disgenet-score sub attribute, value double;
interaction-type sub attribute, value string;
sentence-text sub attribute, value string;
country-name sub attribute, value string;
name sub attribute, value string; 
published-name sub attribute, value string;

### Rules


rule gene-disease-association-and-gene-protein-encoding-protein-disease-association:
when {
    $g isa gene;
    $pr isa protein;
    $di isa disease;
    $r1 (associated-disease: $di, associated-gene: $g) isa gene-disease-association;
    $r2 (encoding-gene: $g, encoded-protein: $pr) isa gene-protein-encoding;
} then {
    (associated-protein: $pr, associated-disease: $di) isa protein-disease-association;
};

rule gene-tissue-association-gene-protein-encoding-implies-tissue-enhancement:
when {
    $g isa gene;
    $pr isa protein;
    $ti isa tissue;
    $r1 (associated-tissue: $ti, associated-gene: $g) isa gene-tissue-association;
    $r2 (encoding-gene: $g, encoded-protein: $pr) isa gene-protein-encoding;
} then {
    (expressed-protein: $pr, enhanced-tissue: $ti) isa tissue-enhancement;
};

rule drug-gene-interaction-and-gene-protein-encoding-implies-drug-protein-interaction:
when {
    $g isa gene;
    $pr isa protein;
    $dr isa drug;
    $r1 ($dr, target-gene: $g) isa drug-gene-interaction;
    $r2 (encoding-gene: $g, encoded-protein: $pr) isa gene-protein-encoding;
} then {
    (target-protein: $pr, inhibitor: $dr) isa drug-protein-interaction;
};

rule protein-disease-association-and-tissue-enhancement-implies-disease-tissue-association:
when {
   $di isa disease;
   $pr isa protein;
   $ti isa tissue;
   $r1 (associated-disease: $di, associated-protein: $pr) isa protein-disease-association;
   $r2 (expressed-protein: $pr, enhanced-tissue: $ti) isa tissue-enhancement;
} then {
   (associated-disease: $di, associated-tissue: $ti) isa disease-tissue-association;
};

rule drug-gene-interaction-and-gene-tissue-association-implies-drug-tissue-association:
when {
   $dr isa drug;
   $ti isa tissue;
   $ge isa gene;
   $r1 (interacting-drug: $dr, target-gene: $ge) isa drug-gene-interaction;
   $r2 (associated-gene: $ge, associated-tissue: $ti) isa gene-tissue-association;
} then {
   (associated-drug: $dr, associated-tissue: $ti) isa drug-tissue-association;
};

rule virus-gene-then-virus-protein:
when {
    $g isa gene; 
    $v isa virus;
    $p isa protein; 
    $1 (associated-virus-gene: $g, associated-virus: $v) isa gene-virus-association;
    $2 (encoding-gene: $g, encoded-protein: $p) isa gene-protein-encoding; 
} then {
    (hosting-virus-protein: $p, associated-virus: $v) isa protein-virus-association;
};

rule inferred-gene-expression:
    when {
        $t isa tissue;
        $c isa cell;
        $g isa gene;
        (composed-tissue: $t, composing-cell: $c) isa composition;
        (expressing-cell: $c, expressed-gene: $g) isa expression;
    } then {
        (expressing-tissue: $t, expressed-gene: $g) isa expression;
    };
