define

cell sub entity,
    owns cell-name,
    plays expression:expressing-cell,
    plays composition:composing-cell,
    plays process-localisation:cell-context;

tissue sub entity,
    owns tissue-name,
    plays tissue-enhancement:enhanced-tissue,
    plays expression:expressing-tissue,
    plays composition:composed-tissue,
    plays process-localisation:tissue-context,
    plays disease-tissue-association:associated-tissue,
    plays drug-tissue-association:associated-tissue;

gene sub entity,
    owns primary-gene-symbol,
    owns alternative-gene-symbol,
    owns entrez-id,
    owns ensembl-gene-id,
    plays expression:expressed-gene,
    plays gene-gene-interaction:interacting-gene,
    plays gene-protein-encoding:encoding-gene,
    plays transcription:transcribing-gene,
    plays gene-disease-association:associated-gene,
    plays drug-gene-interaction:target-gene,
    plays gene-virus-association:associated-virus-gene,
    plays gene-inhibition:inhibited-gene,
    plays gene-inhibition:inhibiting-gene,
    plays gene-interaction:interacted-gene,
    plays gene-interaction:interacting-gene,
    plays gene-coexistance:coexisting-gene,
    plays gene-comparison:comparing-gene,
    plays gene-comparison:compared-gene,
    plays gene-stimulation:stimulating-gene,
    plays gene-stimulation:stimulated-gene,
    plays gene-conversion:converting-gene,
    plays gene-conversion:converted-gene,
    plays gene-production:producing-gene,
    plays gene-production:produced-gene,
    plays gene-similarity:similar-gene,
    plays gene-location:locating-gene,
    plays gene-location:located-gene,
    plays gene-constitution:constituting-gene,
    plays gene-constitution:constituted-gene,
    plays gene-disruption:disrupting-gene,
    plays gene-disruption:disrupted-gene,
    plays gene-augmentation:augmenting-gene,
    plays gene-augmentation:augmented-gene,
    plays gene-affection:affecting-gene,
    plays gene-affection:affected-gene,
    plays gene-association:associated-gene,
    plays gene-association:associating-gene;
transcript sub entity,
    owns ensembl-transcript-id,
    plays transcription:encoded-transcript,
    plays translation:translating-transcript;
protein sub entity,
    owns uniprot-id,
    owns uniprot-entry-name,
    owns primary-uniprot-name,
    owns alternative-uniprot-name,
    owns function-description,
    plays translation:translated-protein,
    plays protein-disease-association:associated-protein,
    plays gene-protein-encoding:encoded-protein,
    plays tissue-enhancement:expressed-protein,
    plays protein-protein-interaction:interacting-protein,
    plays pathway-participation:participating-protein,
    plays drug-protein-interaction:target-protein,
    plays organism-association:associating,
    plays protein-virus-association:hosting-virus-protein;
disease sub entity,
    owns disease-name,
    owns umls-id,
    owns disease-type,
    plays protein-disease-association:associated-disease,
    plays gene-disease-association:associated-disease,
    plays disease-tissue-association:associated-disease,
    plays drug-disease-association:affected-disease;
drug sub entity,
    owns dgidb-name,
    owns chembl-id,
    owns dgidb-claim-name,
    owns drug-claim-source,
    plays drug-protein-interaction:interacting-drug,
    plays drug-gene-interaction:interacting-drug,
    plays drug-disease-association:therapeutic,
    plays drug-tissue-association:associated-drug;
pathway sub entity,
    owns pathway-name,
    owns reactome-id,
    plays pathway-participation:participated-pathway;
organism sub entity,
    owns organism-name,
    plays organism-association:associated-organism,
    plays organism-virus-hosting:hosting-organism;
virus sub organism,
    owns genbank-id,
    owns virus-name,
    owns identity-percentage,
    plays organism-virus-hosting:hosted-virus,
    plays protein-virus-association:associated-virus,
    plays gene-virus-association:associated-virus,
    plays discovery:discovered-virus;
country sub entity,
    owns country-name,
    plays discovery:discovering-location;
journal sub entity,
    owns journal-name,
    plays publishing:publishing-journal;
publication sub entity,
    owns pubmed-id,
    owns title,
    owns doi,
    owns year,
    owns volume,
    owns issn,
    plays mention:mentioning,
    plays authorship:authored-publication,
    plays publishing:published-publication;
person sub entity,
    owns author-name,
    plays authorship:author;

## Relations
transcription sub relation,
    relates transcribing-gene,
    relates encoded-transcript;
translation sub relation,
    relates translated-protein,
    relates translating-transcript;
gene-protein-encoding sub relation,
    relates encoded-protein,
    relates encoding-gene;
protein-disease-association sub relation,
    relates associated-protein,
    relates associated-disease;
gene-disease-association sub relation,
    owns disgenet-score,
    relates associated-gene,
    relates associated-disease;
drug-protein-interaction sub relation,
    relates interacting-drug,
    relates target-protein;
drug-gene-interaction sub relation,
    owns interaction-type,
    relates target-gene,
    relates interacting-drug;
tissue-enhancement sub relation,
    relates enhanced-tissue,
    relates expressed-protein;
process-localisation sub relation,
    relates biomolecular-process,
    relates tissue-context,
    relates cell-context;
protein-protein-interaction sub relation,
    relates interacting-protein,
    plays process-localisation:biomolecular-process;
drug-disease-association sub relation,
    relates affected-disease,
    relates therapeutic;
pathway-participation sub relation,
    relates participating-protein,
    relates participated-pathway;
disease-tissue-association sub relation,
    relates associated-disease,
    relates associated-tissue;
drug-tissue-association sub relation,
    relates associated-drug,
    relates associated-tissue;
organism-association sub relation,
    relates associated-organism,
    relates associating;
organism-virus-hosting sub relation,
    relates hosted-virus,
    relates hosting-organism;
protein-virus-association sub relation,
    relates associated-virus,
    relates hosting-virus-protein;
gene-virus-association sub relation,
    relates associated-virus,
    relates associated-virus-gene;
discovery sub relation,
    relates discovered-virus,
    relates discovering-location;

expression sub relation,
    owns expression-value,
    owns expression-value-reliability,
    relates expressing-tissue,
    relates expressing-cell,
    relates expressed-gene;

composition sub relation,
    relates composed-tissue,
    relates composing-cell;

gene-relation sub relation,
    relates passive-gene,
    relates active-gene;

gene-inhibition sub gene-relation,
    relates inhibited-gene as passive-gene,
    relates inhibiting-gene as active-gene,
    plays mention:mentioned;

gene-interaction sub gene-relation,
    relates interacted-gene as passive-gene,
    relates interacting-gene as active-gene,
    plays mention:mentioned;

gene-coexistance sub gene-relation,
    relates coexisting-gene as passive-gene,
    plays mention:mentioned;

gene-comparison sub gene-relation,
    relates compared-gene as passive-gene,
    relates comparing-gene as active-gene,
    plays mention:mentioned;

gene-stimulation sub gene-relation,
    relates stimulated-gene as passive-gene,
    relates stimulating-gene as active-gene,
    plays mention:mentioned;

gene-conversion sub gene-relation,
    relates converted-gene as passive-gene,
    relates converting-gene as active-gene,
    plays mention:mentioned;

gene-production sub gene-relation,
    relates produced-gene as passive-gene,
    relates producing-gene as active-gene,
    plays mention:mentioned;

gene-similarity sub gene-relation,
    relates similar-gene as passive-gene,
    plays mention:mentioned;

gene-location sub gene-relation,
    relates located-gene as passive-gene,
    relates locating-gene as active-gene,
    plays mention:mentioned;

gene-constitution sub gene-relation,
    relates constituted-gene as passive-gene,
    relates constituting-gene as active-gene,
    plays mention:mentioned;

gene-disruption sub gene-relation,
    relates disrupted-gene as passive-gene,
    relates disrupting-gene as active-gene,
    plays mention:mentioned;

gene-augmentation sub gene-relation,
    relates augmented-gene as passive-gene,
    relates augmenting-gene as active-gene,
    plays mention:mentioned;

gene-affection sub gene-relation,
    relates affected-gene as passive-gene,
    relates affecting-gene as active-gene,
    plays mention:mentioned;

gene-association sub gene-relation,
    relates associated-gene as passive-gene,
    relates associating-gene as active-gene;

mention sub relation,
    owns source,
    owns sentence-text,
    relates mentioning,
    relates mentioned;

publishing sub relation,
    relates publishing-journal,
    relates published-publication;

authorship sub relation,
    relates author,
    relates authored-publication;

gene-gene-interaction sub relation,
    relates interacting-gene,
    plays process-localisation:biomolecular-process;

## Attributes
# Name attributes:
name sub attribute, abstract, value string;
disease-name sub name;
disease-type sub attribute, value string;
protein-name sub name, abstract;
uniprot-name sub protein-name, abstract;
primary-uniprot-name sub uniprot-name, value string;
alternative-uniprot-name sub uniprot-name, value string;
gene-symbol sub gene-id, abstract;
primary-gene-symbol sub gene-symbol;
alternative-gene-symbol sub gene-symbol;
journal-name sub name;
cell-name sub name;
uniprot-entry-name sub protein-name;
pathway-name sub name;
tissue-name sub name;
drug-name sub name, abstract;
dgidb-name sub drug-name;
organism-name sub name;
virus-name sub name;
dgidb-claim-name sub drug-name;
source sub attribute, value string;
publication-id sub id, abstract;
pubmed-id sub publication-id;
publication-name sub name, abstract;
title sub publication-name;
year sub attribute, value long;
volume sub attribute, value string;
issn sub publication-id;
doi sub publication-id;


id sub attribute, abstract, value string;
gene-id sub id, abstract;
ensembl-gene-id sub gene-id;
entrez-id sub gene-id;
protein-id sub id, abstract;
uniprot-id sub protein-id;
transcript-id sub id, abstract;
ensembl-transcript-id sub transcript-id;
disease-id sub id, abstract;
umls-id sub disease-id;
virus-id sub id, abstract;
genbank-id sub virus-id;
pathway-id sub id, abstract;
reactome-id sub pathway-id;
drug-id sub id, abstract;
chembl-id sub drug-id;



expression-value sub attribute, value string;
expression-value-reliability sub attribute, value string;
drug-claim-source sub attribute, value string;
identity-percentage sub attribute, value double;
function-description sub attribute, value string;
disgenet-score sub attribute, value double;
interaction-type sub attribute, value string;
sentence-text sub attribute, value string;
country-name sub name;
author-name sub name;

### Rules


rule gene-disease-association-and-gene-protein-encoding-protein-disease-association:
when {
    $g isa gene;
    $pr isa protein;
    $di isa disease;
    $r1 (associated-disease: $di, associated-gene: $g) isa gene-disease-association;
    $r2 (encoding-gene: $g, encoded-protein: $pr) isa gene-protein-encoding;
} then {
    (associated-protein: $pr, associated-disease: $di) isa protein-disease-association;
};

rule gene-tissue-expression-gene-protein-encoding-implies-tissue-enhancement:
when {
    $g isa gene;
    $pr isa protein;
    $ti isa tissue;
    $r1 (expressing-tissue: $ti, expressed-gene: $g) isa expression;
    $r2 (encoding-gene: $g, encoded-protein: $pr) isa gene-protein-encoding;
} then {
    (expressed-protein: $pr, enhanced-tissue: $ti) isa tissue-enhancement;
};

rule drug-gene-interaction-and-gene-protein-encoding-implies-drug-protein-interaction:
when {
    $g isa gene;
    $pr isa protein;
    $dr isa drug;
    $r1 ($dr, target-gene: $g) isa drug-gene-interaction;
    $r2 (encoding-gene: $g, encoded-protein: $pr) isa gene-protein-encoding;
} then {
    (target-protein: $pr, interacting-drug: $dr) isa drug-protein-interaction;
};

rule protein-disease-association-and-tissue-enhancement-implies-disease-tissue-association:
when {
   $di isa disease;
   $pr isa protein;
   $ti isa tissue;
   $r1 (associated-disease: $di, associated-protein: $pr) isa protein-disease-association;
   $r2 (expressed-protein: $pr, enhanced-tissue: $ti) isa tissue-enhancement;
} then {
   (associated-disease: $di, associated-tissue: $ti) isa disease-tissue-association;
};

rule drug-gene-interaction-and-gene-tissue-expression-implies-drug-tissue-association:
when {
   $dr isa drug;
   $ti isa tissue;
   $ge isa gene;
   $r1 (interacting-drug: $dr, target-gene: $ge) isa drug-gene-interaction;
   $r2 (expressed-gene: $ge, expressing-tissue: $ti) isa expression;
} then {
   (associated-drug: $dr, associated-tissue: $ti) isa drug-tissue-association;
};

rule virus-protein-then-virus-gene:
when {
    $g isa gene;
    $v isa virus;
    $p isa protein;
    $1 (hosting-virus-protein: $p, associated-virus: $v) isa protein-virus-association;
    $2 (encoding-gene: $g, encoded-protein: $p) isa gene-protein-encoding;
} then {
    (associated-virus-gene: $g, associated-virus: $v) isa gene-virus-association;
};

rule inferred-gene-expression:
    when {
        $t isa tissue;
        $c isa cell;
        $g isa gene;
        (composed-tissue: $t, composing-cell: $c) isa composition;
        (expressing-cell: $c, expressed-gene: $g) isa expression;
    } then {
        (expressing-tissue: $t, expressed-gene: $g) isa expression;
    };

rule inferred-tissue-context:
    when {
        $g1 isa gene;
        $g2 isa gene;
        $i (interacting-gene: $g1, interacting-gene: $g2) isa gene-gene-interaction;
        $c isa cell;
        (biomolecular-process: $i, cell-context: $c) isa process-localisation;
        $t isa tissue;
        (composed-tissue: $t, composing-cell: $c) isa composition;
    } then {
        (biomolecular-process: $i, tissue-context: $t) isa process-localisation;
    };

rule inferred-protein-interaction:
    when {
        $g1 isa gene;
        $p1 isa protein;
        (encoding-gene: $g1, encoded-protein: $p1) isa gene-protein-encoding;
        $g2 isa gene;
        $p2 isa protein;
        (encoding-gene: $g2, encoded-protein: $p2) isa gene-protein-encoding;
        (interacting-gene: $g1, interacting-gene: $g2) isa gene-gene-interaction;
    } then {
        (interacting-protein: $p1, interacting-protein: $p2) isa protein-protein-interaction;
    };

rule inferred-protein-interaction-context:
    when {
        $g1 isa gene;
        $p1 isa protein;
        (encoding-gene: $g1, encoded-protein: $p1) isa gene-protein-encoding;
        $g2 isa gene;
        $p2 isa protein;
        (encoding-gene: $g2, encoded-protein: $p2) isa gene-protein-encoding;
        $gi (interacting-gene: $g1, interacting-gene: $g2) isa gene-gene-interaction;
        {
            $c isa tissue;
            $context type process-localisation:tissue-context;
        } or {
            $c isa cell;
            $context type process-localisation:cell-context;
        };
        (biomolecular-process: $gi, $context: $c) isa process-localisation;
        $pi (interacting-protein: $p1, interacting-protein: $p2) isa protein-protein-interaction;
    } then {
        (biomolecular-process: $pi, $context: $c) isa process-localisation;
    };
